def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

// Find the `keystore.jks` and `keystore.properties` files in
// Azure DevOps server. The files should be uploaded using
// the task `Download Secure File`.
def keyStoreProperties = new Properties()
def keyStorePropertiesFilePath = System.getenv("AGENT_TEMPDIRECTORY") + "/" + "smart_notes_keystore.properties"
def keyStorePropertiesFile = file(keyStorePropertiesFilePath)
def keyStoreFilePath = System.getenv("AGENT_TEMPDIRECTORY") + "/" + "smart_notes_keystore.jks"
def keyStoreFile = file(keyStoreFilePath)
def canSignWithCIKeyStore = keyStoreFile.exists() && keyStorePropertiesFile.exists()
if (canSignWithCIKeyStore) {
    keyStoreProperties.load(new FileInputStream(keyStorePropertiesFile))
}

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.crosssolutions.enotes.hackathon"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if(canSignWithCIKeyStore) {
            releaseCI {
                storeFile keyStoreFile
                keyAlias keyStoreProperties['keyAlias']
                keyPassword keyStoreProperties['keyPassword']
                storePassword keyStoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            if (canSignWithCIKeyStore) {
                println 'Signing with provided keystore file'
                signingConfig signingConfigs.releaseCI
            } else {
                println 'Signing with debug keystore file'
                signingConfig signingConfigs.debug
            }
            debuggable false
            minifyEnabled false
            zipAlignEnabled true
        }

        debug {
            zipAlignEnabled true
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.google.firebase:firebase-ml-vision:19.0.3'
}

apply plugin: 'com.google.gms.google-services'
